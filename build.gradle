plugins {
    id 'java'
    id "com.github.node-gradle.node" version "7.0.2"
    id "io.freefair.lombok" version "8.0.1"
    id "run.halo.plugin.devtools" version "0.4.1"
}

group 'site.muyin.picturebed'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.20.11-SNAPSHOT')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    implementation 'cn.hutool:hutool-all:5.8.25'

}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

node {
    nodeProjectDir = file("${project.projectDir}/ui")
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']
    dependsOn('installDepsForUI')
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']
}

build {
    // build frontend before build
    tasks.named('compileJava').configure {
        dependsOn('buildFrontend')
    }
}

halo {
    version = '2.20.12'
    debug = true
}

haloPlugin {
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        groupingRules {
            pictureBedApis {
                displayName = 'Extension API for PictureBed Plugin'
                pathsToMatch = [
                        '/apis/picturebed.muyin.site/v1alpha1/**'
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/pictureBedApis': 'pictureBedApis.json'
        ]
        generator {
            outputDir = file("${projectDir}/ui/src/api/generated")
        }
    }
}